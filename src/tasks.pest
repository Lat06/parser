/// Правило для статусу виконання завдання: "x" (виконано) або " " (в очікуванні).
status = { "x" | " " }

/// Правило для визначення пріоритету завдання, наприклад, "(high)".
priority = { "(" ~ priority_inner ~ ")" }

/// Внутрішнє правило для значень пріоритету: 'low', 'medium' або 'high'.
priority_inner = @{ "low" | "medium" | "high" }

/// Внутрішнє правило для вмісту тегу (без символу '#').
tag_inner = @{ (!" " ~ !NEWLINE ~ ANY)+ }

/// Правило для повного тегу, наприклад, "#rust".
tag = { "#" ~ tag_inner }

/// Текст опису завдання. Використовує негативний перегляд, щоб не включити пріоритети або теги.
description_text = @{
    (!(NEWLINE) ~ !(" " ~ "(") ~ !(" " ~ "#") ~ ANY)+
}

/// ! Головне правило: один повний рядок завдання.
task_line = {
    "-" ~ " " ~ "[" ~ status ~ "]" ~ " " ~ description_text ~ (" " ~ priority)? ~ (" " ~ tag)* ~ (NEWLINE | EOI)
}

/// Правило, що охоплює весь файл: початок, нуль або більше рядків завдань/порожніх рядків, і кінець файлу.
file = {
    SOI ~ (task_line | NEWLINE)* ~ EOI
}
